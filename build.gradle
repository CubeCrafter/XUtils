plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'dev.pixelstudios'
version = '1.9.11'

repositories {
    mavenCentral()

    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/groups/public/' }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url 'https://repo.codemc.org/repository/maven-public/' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://mvn.lumine.io/repository/maven-public/' }
    maven { url 'https://repo.oraxen.com/releases' }
    maven { url 'https://repo.auxilor.io/repository/maven-public/' }
    maven { url 'https://maven.enginehub.org/repo/' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.20.4-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.5'
    compileOnly 'de.tr7zw:item-nbt-api:2.13.2'
    compileOnly 'org.projectlombok:lombok:1.18.32'
    compileOnly 'net.Indyuce:MMOItems:6.7.3'
    compileOnly 'io.lumine:MythicLib-dist:1.4'
    compileOnly 'com.github.LoneDev6:API-ItemsAdder:3.6.3-beta-14'
    compileOnly 'io.th0rgal:oraxen:1.174.0'
    compileOnly 'com.willfp:eco:6.70.0'
    compileOnly 'com.sk89q:worldguard:6.1.1-SNAPSHOT'
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.5'

    // compileOnly fileTree(dir: 'libs', include: ['*.jar'])

    api 'com.github.cryptomorin:XSeries:11.2.1'

    annotationProcessor 'org.projectlombok:lombok:1.18.32'
}

shadowJar {
    exclude 'META-INF/**'
    minimize()
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

processResources {
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand version: version
    }
}

tasks.register('printVersion') {
    doLast {
        println version
    }
}
