plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'me.cubecrafter'
version = '1.1.2'

repositories {
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/groups/public/' }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation 'com.github.cryptomorin:XSeries:9.4.0'

    compileOnly 'org.spigotmc:spigot-api:1.20.1-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.2'
    compileOnly 'org.projectlombok:lombok:1.18.26'

    annotationProcessor 'org.projectlombok:lombok:1.18.26'
}

shadowJar {
    relocate 'com.cryptomorin.xseries', 'me.cubecrafter.xutils.libs.xseries'
    exclude 'META-INF/**'
    minimize()
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(8))
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

processResources {
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand version: version
    }
}